
MEMORY
{
    RAM : ORIGIN = 0x8000, LENGTH = 0x10000000
}

/* heap_size = 128M; */
 __heap_size__ = 1M;
 __stack_size__ = 512K; /* stack size is 128K per core for 4 cores */



SECTIONS
{
	.text : { 
		*(.text*) 
		*(.rodata)
		*(.rodata.*)
		__text_end__ = .;
		. = ALIGN (8);
	} > RAM
	__data_rom_start__ = .;

	.data : {
		__data_start__ = .;
		*(.data .data.*)
		. = ALIGN (8);
		__data_end__ = .;
	} > RAM
	__data_size__ = __data_end__ - __data_start__;

	__exidx_start = .;
	.ARM.exidx : {
		*(.ARM.exidx* .gnu.linkonce.armexidx.*)
	} > RAM
	. = ALIGN(8);
	__exidx_end = .;

	.bss  : {
		__bss_start__ = .;
		*(.bss .bss.*)
		*(COMMON)
		. = ALIGN(8);
		__bss_end__ = .;
	} > RAM
	__bss_size__ = __bss_end__ - __bss_start__;

	.heap : {
		. = ALIGN(8);
		__heap_start__ = .;
		*(.heap*)
		. += __heap_size__;
		. = ALIGN(8);
		__heap_limit__ = .;
	} > RAM
	
	.stack : {
		. = ALIGN(8);
		__stack_start__ = .;
		*(.stack*)
		. += __stack_size__;
		. = ALIGN(8);
		__stack_limit__ = .;
	} > RAM
}
 
